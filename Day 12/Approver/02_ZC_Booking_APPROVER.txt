@EndUserText.label: 'Booking projection view'
@AccessControl.authorizationCheck: #CHECK
@UI: {
  headerInfo: { typeName: 'Booking',
                typeNamePlural: 'Bookings',
                title: { type: #STANDARD, value: 'BookingID' }
  }
}

@Search.searchable: true
@Metadata.allowExtensions: true
define view entity ZC_MT_XX_BOOKING_APPROVER_M
  as projection on ZI_MT_XX_LAM_BOOKING_M
{
  key TravelId,
  key BookingId,
      BookingDate,
      @Consumption.valueHelpDefinition: [{ entity: { name:
       '/DMO/I_Customer', element: 'CustomerID' } }]
      @ObjectModel.text.element: ['CustomerName']
      CustomerId,
      @Semantics.text: true
      _Customer.FirstName as CustomerName,
      @Consumption.valueHelpDefinition: [{ entity: { name: 
      '/DMO/I_Carrier', element: 'AirlineID' } }]
      @ObjectModel.text.element: ['CarrierName']
      CarrierId,
      @Semantics.text: true
      _Carrier.Name       as CarrierName,
      @Consumption.valueHelpDefinition: [{ entity: { name:
       '/DMO/I_Flight', element: 'ConnectionID' },
      additionalBinding: [{ localElement: 'FlightDate', element: 'FlightDate' },
      { localElement: 'CarrierID', element: 'AirlineID' },
      { localElement: 'FlightPrice', element: 'Price' },
      { localElement: 'CurrencyCode', element: 'CurrencyCode' }] }]
      ConnectionId,
      @Consumption.valueHelpDefinition: [{ entity: { name:
       '/DMO/I_Flight', element: 'FlightDate' },
      additionalBinding: [{ localElement: 'ConnectionID', element: 'ConnectionID' },
      { localElement: 'CarrierID', element: 'AirlineID' },
      { localElement: 'FlightPrice', element: 'Price' },
      { localElement: 'CurrencyCode', element: 'CurrencyCode' }] }]
      FlightDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      FlightPrice,
      CurrencyCode,
      Lastchangedat,
      /* Associations */
      _Carrier,
      _Connection,
      _Customer,
      _Travel : redirected to parent ZC_MT_XX_TRAVEL_APPROVER_M
}
