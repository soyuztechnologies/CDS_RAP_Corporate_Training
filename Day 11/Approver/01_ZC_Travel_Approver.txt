@EndUserText.label: 'Travel projection view'
@AccessControl.authorizationCheck: #CHECK
@UI: {
  headerInfo: { typeName: 'Travel', typeNamePlural: 'Travels', 
  title: { type: #STANDARD, value: 'TravelID' } } }
@Metadata.allowExtensions: true
@Search.searchable: true
define root view entity ZC_MT_XX_TRAVEL_APPROVER_M
  as projection on ZI_MT_XX_TRAVEL_M
{
  key TravelId,
      @Consumption.valueHelpDefinition: [{ entity:
       { name: '/DMO/I_Agency', element: 'AgencyID' } }]
      @ObjectModel.text.element: ['AgencyName']
      AgencyId,
      @Semantics.text: true
      _Agency.Name        as AgencyName,
      @Consumption.valueHelpDefinition: [{ entity: { name:
       '/DMO/I_Customer', element: 'CustomerID' } }]
      @ObjectModel.text.element: ['CustomerName']
      CustomerId,
      @Semantics.text: true
      _Customer.FirstName as CustomerName,
      BeginDate,
      EndDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      BookingFee,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      TotalPrice,
      CurrencyCode,
      Memo,
      Status,
      Createdby,
      Createdat,
      Lastchangedby,
      Lastchangedat,
      /* Associations */
      _Agency,
      _Booking : redirected to composition child ZC_MT_XX_BOOKING_APPROVER_M,
      _Currency,
      _Customer
}
